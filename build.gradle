plugins {
    id 'java'
    id 'application'
}

repositories {
    mavenLocal()
    maven {
        url = 'https://repo.runelite.net'
    }
    mavenCentral()
}

def runeLiteVersion = '1.8.13.1'

// Test locally via Gradle: "./gradlew run -DgimpDev=true"
final boolean isGimpDev = Boolean.TRUE.toString().equalsIgnoreCase(System.getProperty("gimpDev"));

dependencies {
    if (isGimpDev) {
        implementation group: 'net.runelite', name: 'client', version: runeLiteVersion
    } else {
        compileOnly group: 'net.runelite', name: 'client', version: runeLiteVersion
    }

    compileOnly 'org.projectlombok:lombok:1.18.20'
    annotationProcessor 'org.projectlombok:lombok:1.18.20'

    testImplementation 'junit:junit:4.12'
    testImplementation group: 'net.runelite', name: 'client', version: runeLiteVersion
    testImplementation group: 'net.runelite', name: 'jshell', version: runeLiteVersion

    implementation('io.socket:socket.io-client:2.0.1') {
        exclude group: 'com.squareup.okhttp3', module: 'okhttp'
    }
}

group = 'com.gimp'
version = '2.2.2'
sourceCompatibility = isGimpDev ? '11' : '1.8';

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

// Allows us to use the plugin via the Gradle "run" task
application {
    mainClassName = 'com.gimp.GimPlugin'
}

run {
    enableAssertions = true
}
